!disclaimer
subroutine writeCoordinatesPSF(iounit)
    use moduleVariables
    use moduleSystem 
    use moduleElements

    implicit none
    integer, intent(in) :: iounit

    integer :: i, id, jd
    character(len=50) :: fmt
    fmt='(I8,1X,A4,1X,I4,1X,A4,1X,A4,1X,A4,1X,2G14.6,I8)'

    write(iounit,'(a)')"PSF"
    write(iounit,*)
    write(iounit,'(a)') "       9 !NTITLE"
    write(iounit,'(a)') " REMARKS file generated by GPTA"
    write(iounit,*)
    write(iounit,'(i8,1x,a)') frame % natoms , "!NATOM"
    do i=1,frame % natoms
      id = getAtomicNumber(frame % lab(i))
      jd = atomToMoleculeIndex(i)
      write(iounit,fmt) i, adjustl(listOfMolecules(jd) % resname), listOfMolecules(jd) % ID , &
        listOfMolecules(jd) % resname, frame % lab(i), atom(id)%lab, frame % chg(i), atom(id)%mass, 0
    enddo
    
    write(iounit,*)
    write(iounit,'(i8,1x,a)') numberOfUniqueBonds , "!NBOND: bonds"
    write(iounit,'(8(i8,x))')listOfUniqueBonds
    
    write(iounit,*)
    write(iounit,'(i8,1x,a)') numberOfUniqueAngles , "!NTHETA: angles"
    write(iounit,'(9(i8,x))')listOfUniqueAngles

    write(iounit,*)
    write(iounit,'(i8,1x,a)') numberOfUniqueTorsions , "!NPHI: dihedrals"
    write(iounit,'(8(i8,x))')listOfUniqueTorsions
    
    write(iounit,*)
    write(iounit,'(i8,1x,a)') numberOfUniqueOutOfPlane , "!NIMPHI: impropers"
    write(iounit,'(8(i8,x))')listOfUniqueOutOfPlane
    
  end subroutine writeCoordinatesPSF

! The PSF file contains six main sections of interest: atoms, bonds, angles, dihedrals, impropers (dihedral force terms used to maintain planarity), and cross-terms. The following is taken from a PSF file for ubiquitin. First is the title and atom records:

! PSF CMAP

!        6 !NTITLE
!  REMARKS original generated structure x-plor psf file
!  REMARKS 2 patches were applied to the molecule.
!  REMARKS topology top_all27_prot_lipid.inp
!  REMARKS segment U { first NTER; last CTER; auto angles dihedrals }
!  REMARKS defaultpatch NTER U:1
!  REMARKS defaultpatch CTER U:76

!     1231 !NATOM
!        1 U    1    MET  N    NH3   -0.300000       14.0070           0
!        2 U    1    MET  HT1  HC     0.330000        1.0080           0
!        3 U    1    MET  HT2  HC     0.330000        1.0080           0
!        4 U    1    MET  HT3  HC     0.330000        1.0080           0
!        5 U    1    MET  CA   CT1    0.210000       12.0110           0
!        6 U    1    MET  HA   HB     0.100000        1.0080           0
!        7 U    1    MET  CB   CT2   -0.180000       12.0110           0
! The fields in the atom section are atom ID, segment name, residue ID, residue name, atom name, atom type, charge, mass, and an unused 0. 
! PSF files may be in either CHARMM or X-PLOR format, with the CHARMM format using an integer rather than a name for the atom type.
! NAMD requires the X-PLOR format, which is also more flexible since it is not tied to the specific order of atom types in a single parameter file. 
! NAMD and VMD require that the order of atoms in any PDB, DCD, or other atomic data file exactly matches the order found in the PSF file.

! Notice that hydrogen atoms are included, that multiple atoms in a residue may share the same type (e.g., HT1, HT2, and HT3 are of type HC), 
! and that atoms of the same element may have different types (e.g., CA and CB, HA, and HT1). Much of the information in the PDB is also included in the PSF file. 
! Often, the only information from the PDB file used by NAMD is the atomic coordinates. 
! In some cases the contents of the occupancy and beta fields of the PDB file are used as parameters to optional simulation methods.

! The covalent bond section lists four pairs of atoms per line:

!     1237 !NBOND: bonds
!        1       5       2       1       3       1       4       1
!        5       6       7       5       7       8       7       9
!       10       7      10      11      10      12      13      10
! The angle section lists three triples of atoms per line:

!     2257 !NTHETA: angles
!        1       5       6       1       5      18       2       1       5
!        2       1       4       2       1       3       3       1       5
!        3       1       4       4       1       5       5      18      19
! The dihedral and improper sections list two quadruples of atoms per line:

!     3293 !NPHI: dihedrals
!        1       5       7      10       1       5       7       8
!        1       5       7       9       1       5      18      20
!        1       5      18      19       2       1       5       7
!      204 !NIMPHI: impropers
!       18       5      20      19      20      18      22      21
!       30      32      27      31      30      27      32      31
!       32      30      33      34      32      30      34      33
! The cross-term sections list two quadruples of atoms per line:

!  74 !NCRTERM: cross-terms
!       18      20      22      35      20      22      35      37
!       35      37      39      54      37      39      54      56
!       54      56      58      74      56      58      74      76
! The order of the atoms within each bond is not significant. 
! The order of atoms within angles, dihedrals, and impropers is significant, but the order can be reversed without affecting the resulting potential. 
! In no case is the relative order of different bonds etc. significant. 
! Since every bond and improper is explicitly listed in the topology file, the atom order within these terms tend to match the original order given. 
! Angles and dihedrals are typically generated automatically and therefore appear in a sorted order.
